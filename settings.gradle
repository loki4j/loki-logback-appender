import static groovy.io.FileVisitResult.*
import static groovy.io.FileType.*

include 'loki-protobuf'
include 'loki-client'
include 'testkit'
include 'loki-logback-appender'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            version('protobuf', '3.24.0')
            version('loki-protobuf', '0.0.1_pb3.24.0')
            version('jacoco', '0.8.10')

            library('loki-protobuf', 'com.github.loki4j', 'loki-protobuf').versionRef('loki-protobuf')
            library('snappy', 'org.xerial.snappy:snappy-java:1.1.10.3')
            library('apache-httpclient4', 'org.apache.httpcomponents:httpclient:4.5.14')
            library('micrometer', 'io.micrometer:micrometer-core:1.9.15')

            library('logback', 'ch.qos.logback:logback-classic:1.2.12')
            library('jackson', 'com.fasterxml.jackson.core:jackson-databind:2.15.2')

            library('junit', 'junit:junit:4.13.2')

            bundle('pluggable', ['loki-protobuf', 'apache-httpclient4', 'micrometer'])
        }
    }
}

def tempProjects = new File(rootDir, 'temp')
if (tempProjects.exists()) {
    tempProjects.traverse(type: DIRECTORIES, maxDepth: 0) { dir ->
        if (new File(dir, 'build.gradle').exists())
            include 'temp:' + dir.getName()
    }
}
