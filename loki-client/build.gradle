plugins {
    id 'java-library'
    id 'jacoco'
    id "com.google.protobuf" version "0.8.17"
}

group = "com.github.loki4j"

repositories {
     mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += ["$buildDir/generated/source/proto/main/java"]
        }
    }
}

dependencies {
    compileOnly libs.bundles.pluggable

    testImplementation libs.junit
    testImplementation libs.bundles.pluggable
}

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

test {
    testLogging {
        events "failed"
        showExceptions true
        showCauses true
        showStackTraces true
        exceptionFormat "full"
    }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${libs.versions.protobuf.get()}"
  }
}

jacoco {
    toolVersion = libs.versions.jacoco.get()
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/protobuf/**')
        }))
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}
