plugins {
    id 'java-library'
    id 'jacoco'
    id "com.google.protobuf" version "0.8.13"
}

group = "com.github.loki4j"

repositories {
     mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += ["$buildDir/generated/source/proto/main/java"]
        }
    }
}

dependencies {
    compileOnly 'com.google.protobuf:protobuf-java:3.15.1'
    compileOnly 'org.xerial.snappy:snappy-java:1.1.8.4'

    testImplementation 'com.google.protobuf:protobuf-java:3.15.1'
    testImplementation 'org.xerial.snappy:snappy-java:1.1.8.4'
    testImplementation 'junit:junit:4.13.2'
}

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

test {
    testLogging {
        events "failed"
        showExceptions true
        showCauses true
        showStackTraces true
        exceptionFormat "full"
    }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.15.1'
  }
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/protobuf/**')
        }))
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}
