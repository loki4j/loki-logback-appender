plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.google.protobuf" version "0.8.19"
}

def pbVersion = rootProject.hasProperty('pbVersion')
    ? rootProject.pbVersion
    : libs.versions.protobuf.get()

group = "com.github.loki4j"
version = "${file("VERSION").text.trim().replace('#.#.#', pbVersion)}"

repositories {
     mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += ["$buildDir/generated/source/proto/main/java"]
        }
    }
}

dependencies {
    api "com.google.protobuf:protobuf-java:${pbVersion}"
    api libs.snappy

    testImplementation libs.junit
    testImplementation libs.bundles.pluggable
}

compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${pbVersion}"
  }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'loki-protobuf'
            from components.java
            pom {
                name = 'Compiled Protobuf files for Loki4j'
                description = 'Loki4j aims to be the fastest and the most lightweight implementation of Logback Appender for Loki'
                url = 'https://github.com/loki4j/loki-logback-appender'
                licenses {
                    license {
                        name = 'BSD Software License, 2-clause version'
                        url = 'https://github.com/loki4j/loki-logback-appender/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'nehaev'
                        name = 'Anton Nehaev'
                        email = "nehaev@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com/loki4j/loki-logback-appender.git'
                    developerConnection = 'scm:git:git@github.com/loki4j/loki-logback-appender.git'
                    url = 'https://github.com/loki4j/loki-logback-appender'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl =
                rootProject.hasProperty('pubToOSSR')
                    ? "https://oss.sonatype.org/content/repositories/snapshots"
                    : "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            if (rootProject.hasProperty('pubToOSSR')) {
                credentials {
                    if (rootProject.hasProperty('ossrhUsername') && rootProject.hasProperty('ossrhPassword')) {
                        username = rootProject.ossrhUsername
                        password = rootProject.ossrhPassword
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
