plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id "com.google.protobuf" version "0.8.13"
}

group = "com.github.loki4j"
version = "${file("VERSION").text.trim()}"

repositories {
     mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += ["$buildDir/generated/source/proto/main/java"]
        }
    }
}

dependencies {
    api 'ch.qos.logback:logback-classic:1.2.3'

    compileOnly 'com.google.protobuf:protobuf-java:3.12.4'
    compileOnly 'org.xerial.snappy:snappy-java:1.1.8'
    compileOnly 'org.apache.httpcomponents:httpclient:4.5.13'

    testImplementation 'junit:junit:4.13'
    testImplementation 'com.google.protobuf:protobuf-java:3.12.4'
    testImplementation 'org.xerial.snappy:snappy-java:1.1.8'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.13'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
}

sourceCompatibility = 11

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

test {
    testLogging {
        events "failed"
        showExceptions true
        showCauses true
        showStackTraces true
        exceptionFormat "full"
    }
    useJUnit {
        excludeCategories 'com.github.loki4j.SlowTests'
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    exclude '*.proto'
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.12.4'
  }
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/protobuf/**')
        }))
    }
}

task integTests(type: Test) {
    useJUnit {
        includeCategories 'com.github.loki4j.logback.integration.IntegrationTests'
    }
}

task perfTests(type: Test) {
    useJUnit {
        includeCategories 'com.github.loki4j.logback.performance.PerformanceTests'
    }
    forkEvery = 1
    minHeapSize = "2G"
    maxHeapSize = "3G"
    testLogging.showStandardStreams = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'loki-logback-appender'
            from components.java
            pom {
                name = 'Logback Appender for Loki'
                description = 'Loki4j aims to be the fastest and the most lightweight implementation of Logback Appender for Loki'
                url = 'https://github.com/loki4j/loki-logback-appender'
                licenses {
                    license {
                        name = 'BSD Software License, 2-clause version'
                        url = 'https://github.com/loki4j/loki-logback-appender/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'nehaev'
                        name = 'Anton Nehaev'
                        email = "nehaev@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com/loki4j/loki-logback-appender.git'
                    developerConnection = 'scm:git:git@github.com/loki4j/loki-logback-appender.git'
                    url = 'https://github.com/loki4j/loki-logback-appender'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl =
                rootProject.hasProperty('pubToOSSR')
                    ? "https://oss.sonatype.org/content/repositories/snapshots"
                    : "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            if (rootProject.hasProperty('pubToOSSR')) {
                credentials {
                    if (rootProject.hasProperty('ossrhUsername') && rootProject.hasProperty('ossrhPassword')) {
                        username = rootProject.ossrhUsername
                        password = rootProject.ossrhPassword
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

apply from: 'gradle/java8.gradle'

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
