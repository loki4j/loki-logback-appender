plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.google.protobuf" version "0.8.13"
}

group = "com.github.loki4j"
version = "${file("VERSION").text.trim()}"

repositories {
     mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += ["$buildDir/generated/source/proto/main/java"]
        }
    }
    java8 {
        compileClasspath = sourceSets.main.compileClasspath
        java {
            srcDirs = sourceSets.main.java.srcDirs
            exclude '**/LokiJavaHttpAppender.java'
        }
    }
}

dependencies {
    api 'ch.qos.logback:logback-classic:1.2.3'

    compileOnly 'com.google.protobuf:protobuf-java:3.12.4'
    compileOnly 'org.xerial.snappy:snappy-java:1.1.8'
    compileOnly 'org.apache.httpcomponents:httpclient:4.5.13'

    java8AnnotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:0.3.0'

    testImplementation 'junit:junit:4.13'
}

sourceCompatibility = 11

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

compileJava8Java {
    sourceCompatibility = 8
    targetCompatibility = 8

    options.compilerArgs = [
            "--release", "8", // Avoid using Java 9+ APIs
            '--enable-preview',
    ]
    // The following line can be omitted on Java 14 and higher
    options.compilerArgs << '-Xplugin:jabel'

    doFirst {
        options.compilerArgs = options.compilerArgs.findAll {
            it != '--enable-preview'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()

    registerFeature('jdk8') {
        usingSourceSet(sourceSets.java8)
        withJavadocJar()
        withSourcesJar()
    }
}

jar {
    exclude '*.proto'
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.12.4'
  }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'loki-logback-appender'
            from components.java
            pom {
                name = 'Logback Appender for Loki'
                description = 'This project aims to be the fastest and the most lightweight implementation of Logback Appender for Loki'
                url = 'https://github.com/loki4j/loki-logback-appender'
                licenses {
                    license {
                        name = 'BSD Software License, 2-clause version'
                        url = 'https://github.com/loki4j/loki-logback-appender/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'nehaev'
                        name = 'Anton Nehaev'
                        email = "nehaev@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com/loki4j/loki-logback-appender.git'
                    developerConnection = 'scm:git:git@github.com/loki4j/loki-logback-appender.git'
                    url = 'https://github.com/loki4j/loki-logback-appender'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl =
                rootProject.hasProperty('pubToOSSR')
                    ? "https://oss.sonatype.org/content/repositories/snapshots"
                    : "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            if (rootProject.hasProperty('pubToOSSR')) {
                credentials {
                    if (rootProject.hasProperty('ossrhUsername') && rootProject.hasProperty('ossrhPassword')) {
                        username = rootProject.ossrhUsername
                        password = rootProject.ossrhPassword
                    }
                }
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

signing {
    sign publishing.publications.mavenJava
}
