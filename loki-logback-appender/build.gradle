plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
}

group = "com.github.loki4j"
version = "${file("VERSION").text.trim()}"

evaluationDependsOn(":testkit")

repositories {
     mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += [files(project(":loki-client").sourceSets.main.java.srcDirs)]
        }
    }
    test {
        java {
            srcDirs += [files(project(":testkit").sourceSets.main.java.srcDirs)]
        }
    }
}

dependencies {
    api libs.logback

    compileOnly libs.bundles.pluggable

    testImplementation libs.junit
    testImplementation libs.bundles.pluggable
    testImplementation libs.jackson
}

compileJava {
    dependsOn ':loki-client:generateProto'
    
    sourceCompatibility = 11
    targetCompatibility = 11
}

test {
    testLogging {
        showExceptions = true
        showCauses = true
        showStackTraces = true
        exceptionFormat = "full"
        showStandardStreams = true
    }
    useJUnit {
        excludeCategories 'com.github.loki4j.testkit.categories.SlowTests'
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    exclude '*.proto'
}

jacoco {
    toolVersion = libs.versions.jacoco.get()
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

task integTests(type: Test) {
    description = 'Run integration tests'
    useJUnit {
        includeCategories 'com.github.loki4j.testkit.categories.IntegrationTests'
    }
    maxHeapSize = "2G"
    testLogging.exceptionFormat = "full"
}

task perfTests(type: Test) {
    description = 'Run performance tests'
    useJUnit {
        includeCategories 'com.github.loki4j.testkit.categories.PerformanceTests'
    }
    forkEvery = 1
    maxHeapSize = "3G"
    testLogging.showStandardStreams = true
}

task ciOnlyTests(type: Test) {
    description = 'Run CI-bound tests'
    useJUnit {
        includeCategories 'com.github.loki4j.testkit.categories.CIOnlyTests'
    }
    //testLogging.exceptionFormat = "full"
    testLogging.showStandardStreams = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'loki-logback-appender'
            from components.java
            pom {
                name = 'Logback Appender for Loki'
                description = 'Loki4j aims to be the fastest and the most lightweight implementation of Logback Appender for Loki'
                url = 'https://github.com/loki4j/loki-logback-appender'
                licenses {
                    license {
                        name = 'BSD Software License, 2-clause version'
                        url = 'https://github.com/loki4j/loki-logback-appender/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'nehaev'
                        name = 'Anton Nehaev'
                        email = "nehaev@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com/loki4j/loki-logback-appender.git'
                    developerConnection = 'scm:git:git@github.com/loki4j/loki-logback-appender.git'
                    url = 'https://github.com/loki4j/loki-logback-appender'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl =
                rootProject.hasProperty('pubToOSSR')
                    ? "https://oss.sonatype.org/content/repositories/snapshots"
                    : "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            if (rootProject.hasProperty('pubToOSSR')) {
                credentials {
                    if (rootProject.hasProperty('ossrhUsername') && rootProject.hasProperty('ossrhPassword')) {
                        username = rootProject.ossrhUsername
                        password = rootProject.ossrhPassword
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

if (rootProject.hasProperty('java8')) {
    apply from: '../gradle/java8.gradle'
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}
