plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
}

group = "com.github.loki4j"
version = "${file("VERSION").text.trim()}"

repositories {
     mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += [files(project(":loki-client").sourceSets.main.java.srcDirs)]
        }
    }
}

dependencies {
    api libs.logback
    compileOnly libs.bundles.pluggable

    testImplementation project(":testkit")
    testImplementation libs.bundles.pluggable
    testImplementation libs.junit
    testImplementation libs.jackson
    testImplementation libs.logstash

    testRuntimeOnly libs.junitpl
}

compileJava {
    sourceCompatibility = 17
    targetCompatibility = 17
}


test {
    testLogging {
        showExceptions = true
        showCauses = true
        showStackTraces = true
        exceptionFormat = "full"
        showStandardStreams = true
    }
    useJUnitPlatform {
        excludeTags 'integration', 'performance', 'ci-only'
    }
}

task integTests(type: Test) {
    description = 'Run integration tests'
    useJUnitPlatform {
        includeTags 'integration'
    }
    maxHeapSize = "2G"
    testLogging.exceptionFormat = "full"

    testClassesDirs = testing.suites.test.sources.output.classesDirs
    classpath = testing.suites.test.sources.runtimeClasspath
}

task perfTests(type: Test) {
    description = 'Run performance tests'
    useJUnitPlatform {
        includeTags 'performance'
    }
    forkEvery = 1
    maxHeapSize = "3G"
    testLogging.showStandardStreams = true

    testClassesDirs = testing.suites.test.sources.output.classesDirs
    classpath = testing.suites.test.sources.runtimeClasspath
}

task ciOnlyTests(type: Test) {
    description = 'Run CI-bound tests'
    useJUnitPlatform {
        includeTags 'ci-only'
    }
    testLogging.exceptionFormat = "full"
    testLogging.showStandardStreams = true

    testClassesDirs = testing.suites.test.sources.output.classesDirs
    classpath = testing.suites.test.sources.runtimeClasspath
}


java {
    withJavadocJar()
    withSourcesJar()
}

jacoco {
    toolVersion = libs.versions.jacoco.get()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

apply from: '../gradle/publishing.gradle'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'loki-logback-appender'
            pom.name = 'Logback Appender for Loki'
            from components.java
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.deprecation = true
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}
